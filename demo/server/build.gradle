apply plugin: 'application'
apply plugin: 'com.google.protobuf'

buildscript {
    dependencies {
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.9.4")
    }
}

def grpcVersion = '1.61.0' // CURRENT_GRPC_VERSION

dependencies {
    implementation project(':demo:common')
    //implementation 'com.google.capillary:lib:1.0.0'
    implementation project(':lib')
    implementation 'com.google.crypto.tink:tink:1.12.0'
    implementation 'com.google.protobuf:protobuf-java:3.25.2'
    implementation "io.grpc:grpc-netty:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'io.netty:netty-tcnative-boringssl-static:2.0.6.Final'
    implementation 'com.google.api-client:google-api-client:2.2.0'
    implementation 'com.google.code.gson:gson:2.10.1'
    implementation 'com.google.guava:guava:23.4-jre'
    implementation 'commons-cli:commons-cli:1.6.0'
    implementation 'org.xerial:sqlite-jdbc:3.45.0.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
}

sourceSets {
    main {
        proto {
            srcDir project(':demo:common').projectDir.toString() + '/proto'
        }
    }
}

run {
    if (project.hasProperty("runArgs")) {
        args(runArgs.split(','))
    }
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.plugins {
                grpc {
                    option 'enable_deprecated=false'
                }
            }
        }
    }
}

// Inform IntelliJ projects about the generated code.
apply plugin: 'idea'

idea {
    module {
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/grpc")
        sourceDirs += file("${projectDir}/build/generated/source/proto/main/java")
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_17
    targetCompatibility = JavaVersion.VERSION_17
}