apply plugin: 'com.android.application'
apply plugin: 'com.google.protobuf'

buildscript {
    dependencies {
        classpath("com.google.protobuf:protobuf-gradle-plugin:0.9.4")
        classpath 'com.google.gms:google-services:4.4.2'
    }
}

android {
    compileSdk 34
    defaultConfig {
        applicationId "com.google.capillary.demo.android"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 1
        versionName "1.0"
        multiDexEnabled true
    }
    sourceSets {
        main {
            proto {
                srcDir project(':demo:common').projectDir.toString() + '/proto'
            }
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    packagingOptions {
        resources {
            excludes += ['build-data.properties']
        }
    }
    namespace 'com.google.capillary.demo.android'
}

// Checkstyle doesn't run automatically with android
tasks.register('checkstyleMain', Checkstyle) {
    source "src/main/java"
    include '**/*.java'
    classpath = files()
}
check.dependsOn checkstyleMain

def grpcVersion = '1.61.0' // CURRENT_GRPC_VERSION

dependencies {
    implementation project(':demo:common')
    //implementation 'com.google.capillary:lib-android:1.0.0'
    implementation project(":lib-android")
    implementation('com.google.crypto.tink:tink-android:1.12.0', {
        exclude group: 'com.google.protobuf'
    })
    implementation 'androidx.appcompat:appcompat:1.7.0'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'javax.annotation:javax.annotation-api:1.3.2'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'com.google.protobuf:protobuf-java:3.25.2'
    implementation "io.grpc:grpc-okhttp:${grpcVersion}"
    implementation "io.grpc:grpc-protobuf:${grpcVersion}"
    implementation "io.grpc:grpc-stub:${grpcVersion}"
    implementation 'com.google.firebase:firebase-core:21.1.1'
    implementation 'com.google.firebase:firebase-messaging:24.0.0'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'joda-time:joda-time:2.12.6'
    androidTestImplementation 'androidx.multidex:multidex:2.0.1'
    implementation 'com.google.android.gms:play-services-auth:21.2.0'
}

protobuf {
    protoc {
        artifact = 'com.google.protobuf:protoc:3.4.0'
    }
    plugins {
        grpc {
            artifact = "io.grpc:protoc-gen-grpc-java:${grpcVersion}"
        }
    }
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {}
            }
        }
        all()*.plugins {
            grpc {
                option 'enable_deprecated=false'
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
